/* ============================================================================= *
 *    DrAzTiK's revisited fight against Tazok and DigDag : Firkraag's Hideout    *
 * ============================================================================= */

IF
	Global("D9tazokcut","AR1203",0)
THEN
	RESPONSE #100
		TriggerActivation("Tran1202",FALSE)
		CreateVisualEffect("SPSTRENH",[280.485])
		CreateVisualEffect("SPSTRENH",[321.461])
		CreateVisualEffect("SPSTRENH",[360.436])
		CreateVisualEffect("SPSTRENH",[400.405])
		CreateVisualEffect("SPSTRENH",[440.376])
		CreateVisualEffect("SPSTRENH",[480.347])
		CreateVisualEffect("SPSTRENH",[520.319])
		DisplayStringHead(Player1,@321)  // ~A powerful magic barrier is suddenly appearing, it's now impossible to turn back.~
		Wait(3)
		SetGlobal("D9tazokcut","AR1203",1)
		ClearAllActions() 
		StartCutSceneMode()
		SetGlobalTimer("D9GateTimer","AR1203",5)
		StartCutScene("d9cutta")
END


// BARRIÈRE MAGIQUE - MAGIC BARRIER
// --------------------------------
IF
	GlobalTimerExpired("D9GateTimer","AR1203")
	Global("D9tazokdead","AR1203",0)
THEN
	RESPONSE #100
		SetGlobalTimer("D9GateTimer","AR1203",4)
		CreateVisualEffect("SPSTRENH",[280.485])
		CreateVisualEffect("SPSTRENH",[321.461])
		CreateVisualEffect("SPSTRENH",[360.436])
		CreateVisualEffect("SPSTRENH",[400.405])
		CreateVisualEffect("SPSTRENH",[440.376])
		CreateVisualEffect("SPSTRENH",[480.347])
		CreateVisualEffect("SPSTRENH",[520.319])
END


// Removes Firkaag and Conster from AR1203 and create many enemies on the map
// --------------------------------------------------------------------------
IF
	Global("D9removevilains","AR1203",0)
THEN
	RESPONSE #100
		SetGlobal("D9removevilains","AR1203",1)
		ActionOverride("FIRKRA02",DestroySelf())
		ActionOverride("FIRMAG01",DestroySelf())
		CreateCreature("ORC05",[1250.1220],0)     // Orog
		CreateCreature("ORC05",[1250.1220],0)     // Orog
		CreateCreature("ORC01",[1250.1220],0)     // Orc - Orque
		CreateCreature("ORC01",[1250.1220],0)     // Orc - Orque
		CreateCreature("ORC02",[1465.1169],0)     // Orc Archer - Archer Orc
		CreateCreature("ORC02",[1465.1169],0)     // Orc Archer - Archer Orc

		CreateCreature("D9ORC05B",[1638.1488],0)  // Orog
		CreateCreature("D9ORC05",[1638.1488],0)   // Orog

		CreateCreature("D9ORC01",[1200.1327],0)   // Orc - Orque
		CreateCreature("ORC06",[2032.1874],0)     // Orog
		CreateCreature("ORC02",[1995.1979],0)     // Orc Archer - Archer Orc
		CreateCreature("ORC02",[1995.1979],0)     // Orc Archer - Archer Orque
		CreateCreature("D9ORC01",[2080.1860],0)   // Orc - Orque
END

IF
	Global("D9removevilains","AR1203",1)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
THEN
	RESPONSE #100
		CreateCreature("ORC04",[1283.1240],0)    // Orc Mage - Magicien Orque
		CreateCreature("ORC03",[1283.1240],0)    // Orc Priest - Prêtre Orque
		CreateCreature("ORC02",[1205.1348],0)    // Orc Archer - Archer Orc
		CreateCreature("ORC06",[1151.1227],0)    // Orog
		CreateCreature("ORC01",[1250.1220],0)    // Orc - Orque
		CreateCreature("D9ORC01",[1200.1327],0)  // Orc - Orque

		CreateCreature("ORC02",[2285.1775],0)    // Orc Archer - Archer Orc
		CreateCreature("ORC02",[2111.1836],0)    // Orc Archer -  Archer Orc
		CreateCreature("ORC06",[2079.1809],0)    // Orog
		CreateCreature("ORC03",[2079.1809],0)    // Orc Priest - Prêtre Orque
		CreateCreature("ORC04",[2079.1809],0)    // Orc Mage - Magicien Orque
		SetGlobal("D9removevilains","AR1203",2)
END

IF
	Global("D9removevilains","AR1203",2)
	Difficulty(HARDEST)
THEN
	RESPONSE #100
		CreateCreature("ORC04",[1283.1240],0)    // Orc Mage - Magicien Orque
		CreateCreature("ORC02",[1283.1240],0)    // Orc Archer - Archer Orc
		CreateCreature("ORC06",[1151.1227],0)    // Orog
		CreateCreature("ORC03",[1283.1240],0)    // Orc Priest - Prêtre Orque
		
		CreateCreature("ORC02",[2080.1860],0)    // Orc Archer - Archer Orc
		CreateCreature("ORC02",[2111.1836],0)    // Orc Archer - Archer Orc
		CreateCreature("ORC02",[1995.1979],0)    // Orc Archer - Archer Orc
		CreateCreature("ORC02",[2285.1775],0)    // Orc Archer - Archer Orc
		CreateCreature("ORC06",[2079.1809],0)    // Orog
		CreateCreature("ORC04",[2079.1809],0)    // Orc Mage - Magicien Orque
		CreateCreature("ORC03",[2079.1809],0)    // Orc Priest - Prêtre Orque
		SetGlobal("D9removevilains","AR1203",3)
END


// First spawn after second talk with DigDag
// -----------------------------------------
IF
	Global("D9firkfirstspawn","AR1203",1)
THEN
	RESPONSE #100
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		SetGlobal("D9firkfirstspawn","AR1203",2)
END

IF
	Global("D9firkfirstspawn","AR1203",2)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
THEN
	RESPONSE #100
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		SetGlobal("D9firkfirstspawn","AR1203",3)
END

IF
	Global("D9firkfirstspawn","AR1203",3)
	Difficulty(HARDEST)
THEN
	RESPONSE #100
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		SetGlobal("D9firkfirstspawn","AR1203",4)
END


// Second spawn after last talk with DigDag
// ----------------------------------------
IF
	Global("D9firksecondspawn","AR1203",1)
THEN
	RESPONSE #100
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		SetGlobal("D9firksecondspawn","AR1203",2)
END

IF
	Global("D9firksecondspawn","AR1203",2)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
THEN
	RESPONSE #100
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		SetGlobal("D9firksecondspawn","AR1203",3)
END

IF
	Global("D9firksecondspawn","AR1203",3)
	Difficulty(HARDEST)
THEN
	RESPONSE #100
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		CreateCreature("D9ORGAHJ",[425.425],0)  // Orog
		CreateCreature("D9ORGAME",[425.425],0)  // Orog
		SetGlobal("D9firksecondspawn","AR1203",4)
END


// Final fight after DigDag death
// ------------------------------
IF
	Global("D9deaddigdag","AR1203",0)
	Dead("FIRORC01") // Chef DigDag
	CombatCounter(0)
THEN
	RESPONSE #100
		Wait(2)
		SetGlobal("D9deaddigdag","AR1203",1)
		MoveViewPoint([913.994],INSTANT)
		CreateCreature("TAZOK",[913.994],0)     // Tazok
		CreateCreature("HOBSHA01",[913.994],0)  // Hobgobelin Shaman - Chaman Hobgobelin
		CreateCreature("HOBARC02",[913.994],0)  // Hobgobelin Archer - Archer hobgobelin
		CreateCreature("d9groumt",[913.994],0)  // Groumf
		CreateCreature("d9bourt",[913.994],0)   // Bourrinos
		CreateCreature("D9OGRGA",[838.1001],0)  // Berserker Ogre - Ogre Berserker
		CreateCreature("D9OGRGA",[838.1001],0)  // Berserker Ogre - Ogre Berserker
		CreateCreature("HOBARC02",[918.900],0)  // Hobgobelin Archer - Archer hobgobelin
		CreateCreature("HOBARC02",[918.900],0)  // Hobgobelin Archer - Archer hobgobelin
		Wait(2)
		MoveViewObject(Player1,INSTANT)
END

IF
	Global("D9deaddigdag","AR1203",1)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
THEN
	RESPONSE #100
		SetGlobal("D9deaddigdag","AR1203",2)
		CreateCreature("HOBARC02",[822.963],0)  // Hobgobelin Archer - Archer hobgobelin
		CreateCreature("HOBARC02",[862.924],0)  // Hobgobelin Archer - Archer hobgobelin
		CreateCreature("D9OGRGA",[838.1001],0)  // Berserker Ogre - Ogre Berserker
		CreateCreature("ORC03",[970.952],0)     // Orc Priest - Prêtre Orque
		CreateCreature("ORC04",[970.952],0)     // Orc Mage - Magicien Orque
END

IF
	Global("D9deaddigdag","AR1203",2)
	Difficulty(HARDEST)
THEN
	RESPONSE #100
		SetGlobal("D9deaddigdag","AR1203",3)
		CreateCreature("HOBARC02",[822.963],0)  // Hobgobelin Archer - Archer hobgobelin
		CreateCreature("HOBARC02",[862.924],0)  // Hobgobelin Archer - Archer hobgobelin
		CreateCreature("D9OGRGA",[838.1001],0)  // Berserker Ogre - Ogre Berserker
		CreateCreature("ORC04",[970.952],0)     // Orc Mage - Magicien Orque
		CreateCreature("ORC03",[970.952],0)     // Orc Priest - Prêtre Orque
END


// Displays strref when DigDag dies
// --------------------------------
IF
	Global("D9deaddigdagstring","AR1203",0)
	Dead("FIRORC01")
THEN
	RESPONSE #100
		DisplayStringHead(Player1,@322)  // ~DigDag, the hideous orc leader is dead now and his horde will soon stop fighting. However, you have the feeling it can't end up so easily and you should prepare yourself for the last challenge!~
		SetGlobal("D9deaddigdagstring","AR1203",1)
END


// Displays strref when Tazok dies
// -------------------------------
IF
	Global("D9deadtazokstring","AR1203",0)
	Dead("TAZOK")
THEN
	RESPONSE #100
		DisplayStringHead(Player1,@323)  // ~This time, Tazok has definitively rejoined Sarevok in Hell and his minions will not disturb you for a long time.~
		SetGlobal("D9deadtazokstring","AR1203",1)
		SetGlobal("D9deaddigdag","AR1203",2)
END


// Firkraag and Conster appear when Tazok id dead and CombatCounter(0)
// -------------------------------------------------------------------
IF
	Global("D9tazokdead","AR1203",0)
	Dead("TAZOK")
	CombatCounter(0)
THEN
	RESPONSE #100
		MoveViewPoint([1526.1560],INSTANT)
		CreateCreature("observe",[1526.1560],0)
		Wait(1)
		CreateVisualEffect("SPDIMNDR",[1526.1560])
		CreateVisualEffect("SPDIMNDR",[1364.1420])
		Wait(3)
		CreateCreature("FIRKRA02",[1526.1560],0)  // Firkraag
		CreateCreature("FIRMAG01",[1364.1420],0)  // Conster
		ActionOverride("observe",DestroySelf())
		MoveViewObject(Player1,INSTANT)
		TriggerActivation("Tran1202",TRUE)
		SetGlobal("D9tazokdead","AR1203",1)
END
