/* ================================================================== *
 *    Spiders butchery in Watcher's Keep first floor (Altar level)    *
 * ================================================================== */

IF
	Global("D9addvilain","AR3001",0)
THEN
	RESPONSE #100
		SetGlobal("D9addvilain","AR3001",1)
 		CreateCreature("D9IDSW0C",[1504.1245],12)  // Sword Spider - Araignée sabre
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END

IF
	Global("D9beginbutcheryspiders","AR3001",1)
	Global("D9beginbutcheryspidersmessage","AR3001",0)
THEN
	RESPONSE #100
	    DisplayStringHead(Player1,@14661)  // ~Les ennemis semblent arriver de partout !~
		SetGlobal("D9beginbutcheryspidersmessage","AR3001",1)
END

IF
	Global("D9beginbutcheryspiders","AR3001",1)
	GlobalLT("MaxSpawn","AR3001",110)
	GlobalLT("EXTRACOUNT","MYAREA",6)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR3001",1)
		CreateCreatureOffScreen("D9IDPH01",0)  // Phase Spider - Araignée éclipsante
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR3001",1)
		CreateCreatureOffScreen("D9IDSM01",0)  // Small Spider - Petite araignée
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
END

IF
	Global("D9beginbutcheryspiders","AR3001",1)
	GlobalLT("MaxSpawn","AR3001",110)
	GlobalLT("EXTRACOUNT2","MYAREA",6)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR3001",1)
		CreateCreatureOffScreen("D9IDSW01",0)  // Sword Spider - Araignée sabre
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END

IF
	Global("D9beginbutcheryspiders","AR3001",1)
	GlobalLT("MaxSpawn","AR3001",110)
	GlobalLT("EXTRACOUNT3","MYAREA",5)
	Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR3001",1)
		CreateCreatureOffScreen("D9IDSM02",0)  // Mutated Spider - Araignée mutante
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR3001",1)
		CreateCreatureOffScreen("D9IDGI01",0)  // Giant Spider - Araignée géante
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
END
