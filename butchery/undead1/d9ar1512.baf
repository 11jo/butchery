/* ========================================================= *
 *    Undead butchery in Spellhold (Bodhi's Hunt Level 1)    *
 * ========================================================= */

IF
	Global("D9addvilainX","AR1512",0)
THEN
	RESPONSE #100
		SetGlobal("D9addvilainX","AR1512",1)
 		CreateCreature("D9ELARFX",[2152.1819],12)  // Skeleton Archer - Squelette archer
END

IF
	Global("D9beginbutchery2","AR1512",1)
	Global("D9eyelessmessage2","AR1512",0)
THEN
	RESPONSE #100
	    DisplayStringHead(Player1,@14661)  // ~Les ennemis semblent arriver de partout !~
		SetGlobal("D9eyelessmessage2","AR1512",1)
END

IF
	Global("D9beginbutchery2","AR1512",1)
	GlobalLT("MaxSpawn","AR1512",55)
	GlobalLT("EXTRACOUNT","MYAREA",5)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9ELARAA",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9ELARZZ",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
END

IF
	Global("D9beginbutchery2","AR1512",1)
	GlobalLT("MaxSpawn","AR1512",55)
	GlobalLT("EXTRACOUNT2","MYAREA",5)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9ELARYY",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9ELSU11",0)  // Skeleton Warrior - Combattant squelette
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9ELARBB",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END

IF
	Global("D9beginbutchery2","AR1512",1)
	GlobalLT("MaxSpawn","AR1512",55)
	GlobalLT("EXTRACOUNT3","MYAREA",2)
	Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9SM12B",0)   // Experienced Skeleton Mage - Mage squelette expérimenté (12th Level)
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9ELWA",0)    // Skeleton Warrior - Combattant squelette
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9SC12B",0)   // Experienced Skeleton Cleric - Prêtre squelette expérimenté
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1512",1)
		CreateCreatureOffScreen("D9ELARXX",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END
