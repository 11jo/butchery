/* =================================== *
 *    Animals butchery in Trademeet    *
 * =================================== */

IF
	Global("D9addvilain","AR2000",0)
THEN
	RESPONSE #100
		SetGlobal("D9addvilain","AR2000",1)
 		CreateCreature("D9LFDICC",[2727.3415],12)  // Dire Wolf - Loup noir
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
END

IF
	Global("D9beginbutchery","AR2000",1)
	Global("D9eyelessmessage","AR2000",0)
THEN
	RESPONSE #100
	    DisplayStringHead(Player1,@14661)  // ~Les ennemis semblent arriver de partout !~
		SetGlobal("D9eyelessmessage","AR2000",1)
END

IF
	Global("D9beginbutchery","AR2000",1)
	GlobalLT("MaxSpawn","AR2000",70)
	GlobalLT("EXTRACOUNT","MYAREA",6)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9LFWI01",0)  // Winter Wolf - Loup des glaces
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9LFWI01",0)  // Winter Wolf - Loup des glaces
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9IDPH01",0)  // Phase Spider - Araignée éclipsante
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9CATT",0)  // Panther - Panthère
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9CAX",0)  // Spotted Lion - Lion tacheté
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9EARBL",0)  // Black Bear - Ours noir
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
END

IF
	Global("D9beginbutchery","AR2000",1)
	GlobalLT("MaxSpawn","AR2000",70)
	GlobalLT("EXTRACOUNT2","MYAREA",6)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9IDSW01",0)  // Sword Spider - Araignée sabre
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9VBEAE",0)  // Brown Bear - Ours brun
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9AKEB0X",0)  // Boalisk
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9LFDI02",0)  // Dire Wolf - Loup noir
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END

IF
	Global("D9beginbutchery","AR2000",1)
	GlobalLT("MaxSpawn","AR2000",70)
	GlobalLT("EXTRACOUNT3","MYAREA",3)
	Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9IDSM02",0)  // Mutated Spider - Araignée mutante
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9ARPO01",0)  // Polar Bear - Ours de montagne
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR2000",1)
		CreateCreatureOffScreen("D9AKEB0D",0)  // Boalisk
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
END
