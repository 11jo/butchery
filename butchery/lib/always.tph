/* ============================================ *
 *                                              *
 *                 ALWAYS BLOCK                 *
 *                                              *
 * ============================================ */

// Concept borrowed from DavidW's Immutability and encapsulation in mod design ( https://github.com/Gibberlings3/immutable )

/* =================================================================== *
 *  This code only runs once, even if you install lots of components.  *
 * =================================================================== */

ACTION_IF !VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

	SILENT

	/* -------------------------------------------------------------------------------- *
	 *    Determines which game is installed and if the game is an Enhanced Edition.    *
	 * -------------------------------------------------------------------------------- */
	OUTER_SET is_tob   = (GAME_IS ~bgt tob~) ? 1 : 0     // based on vanilla ToB engine
	OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0       // b2gee
	OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0         // eet
	OUTER_SET is_ee    = (GAME_IS ~bg2ee eet~) ? 1 : 0   // EE game


	/* ------------------------------------------------------- *
	 *    Sets default variables if they're not set already    *
	 * ------------------------------------------------------- */

	// Variable used to properly install specific EE games ressources
	ACTION_IF (is_ee) BEGIN
		OUTER_SPRINT ee_file "_ee"
	END ELSE BEGIN
		OUTER_SPRINT ee_file ""
	END


	/* ----------------------------------------------------------------------------------- *
	 *    Animation slot for chieftain orog in EE and classic BG2 have different labels    *
	 * ----------------------------------------------------------------------------------- */

	OUTER_SET slot_halfogre       = IDS_OF_SYMBOL(~ANIMATE~ ~HALF_OGRE~)
	OUTER_SET slot_orogchief      = (is_ee = 1) ? IDS_OF_SYMBOL(~ANIMATE~ ~OROG_CHIEFTAIN~) : IDS_OF_SYMBOL(~ANIMATE~ ~OROG3~)
	OUTER_SET slot_sahuagin_large = (IDS_OF_SYMBOL(~ANIMATE~ ~SAHUAGIN_LARGE~) > 0) ? IDS_OF_SYMBOL(~ANIMATE~ ~SAHUAGIN_LARGE~) : 32547


	/* ------------------------------------------- *
	 *    EET chapters continuity compatibility    *
	 * ------------------------------------------- */
		ACTION_IF is_eet BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END


	/* -------------------------------------------- *
	 *    Loads in any shared function libraries    *
	 * -------------------------------------------- */

	INCLUDE ~%MOD_FOLDER%/lib/functions.tph~


	/* -------------------------- *
	 *    Creates a blank file    *
	 * -------------------------- */

	<<<<<<<< .../butchery-inline/blank

>>>>>>>>


	///////////////////////////////////////////////////////////////////////////////////////
	//    ---------------- Handling of correctly formatted tra-files ----------------    //
	//    Copies tra files into the autotra folder (to leave the originals untouched)    //
	///////////////////////////////////////////////////////////////////////////////////////

	ACTION_IF (!FILE_EXISTS "%MOD_FOLDER%/lang/languages_installed.mrk") BEGIN

		DEFINE_ACTION_FUNCTION ~autotra_workaround~ BEGIN
			MKDIR "%MOD_FOLDER%/lang/autotra/%LANGUAGE%"
			COPY ~%MOD_FOLDER%/lang/%LANGUAGE%~	~%MOD_FOLDER%/lang/autotra/%LANGUAGE%~
		END

		/* Converts tra files to UTF-8 */
		LAF ~autotra_workaround~ END
		
		ACTION_DEFINE_ARRAY fl#noconvert BEGIN setup END
		ACTION_DEFINE_ARRAY fl#reload BEGIN butchery d9firorc END
		LAF HANDLE_CHARSETS 
			INT_VAR	infer_charsets = 1
			STR_VAR	tra_path = EVAL ~%MOD_FOLDER%/lang/autotra~
					iconv_path = EVAL ~%MOD_FOLDER%/lang/iconv~
					noconvert_array = fl#noconvert
					reload_array = fl#reload
		END

		COPY ~.../butchery-inline/blank~ ~%MOD_FOLDER%/lang/languages_installed.mrk~

	END // of ACTION_IF (!FILE_EXISTS "%MOD_FOLDER%/lang/languages_installed.mrk")

	/* TRAs declared in LANGUAGE must be reloaded - by hand, because of the copying (for non-EE version */
	LOAD_TRA ~butchery/lang/autotra/%LANGUAGE%/setup.tra~
	LOAD_TRA ~butchery/lang/autotra/%LANGUAGE%/butchery.tra~
	LOAD_TRA ~butchery/lang/autotra/%LANGUAGE%/d9firorc.tra~

END	// of ACTION_IF !VARIABLE_IS_SET always_once_section BEGIN


/* ================================================================================ *
 *  This code runs for each component to make sure files are not already installed  *
 * ================================================================================ */

/* ----------------------- *
 *    Detects soundsets    *
 * ----------------------- */
OUTER_SET is_soundset = (FILE_EXISTS_IN_GAME ~d9bara1.wav~) ? 1 : 0

/* ----------------------- *
 *    Detects portraits    *
 * ----------------------- */
OUTER_SET is_portraits  = (FILE_EXISTS_IN_GAME ~d9zoks.bmp~) ? 1 : 0
OUTER_SET is_portraits2 = (FILE_EXISTS_IN_GAME ~d9gros.bmp~) ? 1 : 0

/* ------------------------------ *
 *    Detects barbarian script    *
 * ------------------------------ */
OUTER_SET is_d9dwctyz = (FILE_EXISTS_IN_GAME ~d9dwctyz.bcs~) ? 1 : 0

/* ------------------------------- *
 *    Detects Grimwarder script    *
 * ------------------------------- */
OUTER_SET is_d9bodfgh = (FILE_EXISTS_IN_GAME ~d9bodfgh.bcs~) ? 1 : 0

