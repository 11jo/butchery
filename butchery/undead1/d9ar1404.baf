/* ====================================================== *
 *    Undead butchery in Shadow Temple Land (shadowed)    *
 * ====================================================== */

IF
	Global("D9addvilain","AR1404",0)
THEN
	RESPONSE #100
		SetGlobal("D9addvilain","AR1404",1)
 		CreateCreature("D9ELARFF",[4514.174],12)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
END

IF
	Global("D9beginbutchery","AR1404",1)
	Global("D9eyelessmessage","AR1404",0)
THEN
	RESPONSE #100
	    DisplayStringHead(Player1,@14661)  // ~Les ennemis semblent arriver de partout !~
		SetGlobal("D9eyelessmessage","AR1404",1)
END

IF
	Global("D9beginbutchery","AR1404",1)
	GlobalLT("MaxSpawn","AR1404",35)
	GlobalLT("EXTRACOUNT","MYAREA",4)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9ELARAA",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9ELARZZ",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
END

IF
	Global("D9beginbutchery","AR1404",1)
	GlobalLT("MaxSpawn","AR1404",35)
	GlobalLT("EXTRACOUNT2","MYAREA",4)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9ELARBB",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9ELARYY",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9ELSU11",0)  // Skeleton Warrior - Combattant squelette
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END

IF
	Global("D9beginbutchery","AR1404",1)
	GlobalLT("MaxSpawn","AR1404",35)
	GlobalLT("EXTRACOUNT3","MYAREA",2)
	Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9SM7B",0)    // Skeleton Mage novice - Mage squelette novice
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9ELWA",0)    // Skeleton Warrior - Combattant squelette
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9SC12B",0)   // Experienced Skeleton Cleric - Prêtre squelette expérimenté
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR1404",1)
		CreateCreatureOffScreen("D9ELARXX",0)  // Skeleton Archer - Squelette archer
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END
