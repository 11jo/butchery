/* ==================================================================================================================================================== *
 *  v 5.0.0                                                                                                                                              *
 *           B U T C H E R Y    :    D r A z T i K ' s    r e v i s i t e d    f i g h t    a g a i n s t    T a z o k    a n d    D i g D a g           *
 *                                                                                                                                                       *
 * ===================================================================================================================================================== */

SILENT

/* ------------------ *
 *  Installing Items  *
 * ------------------ */

COPY ~%MOD_FOLDER%/items/d9boots.itm~	~override~ // boots providing immunity to backstab for BARBARIAN
COPY ~%MOD_FOLDER%/items/d9nofog.itm~	~override~ // clean fog ring


/* ---------------------- *
 *  Installing Creatures  *
 * ---------------------- */

// Another Tazok for cutscene
// --------------------------
COPY_EXISTING ~tazok.cre~  ~override/d9tazok.cre~
	ADD_CRE_ITEM ~d9nofog~ #0 #0 #0 ~NONE~ ~RING~  // Adds a fog ring to d9tazok
	WRITE_LONG 0x028 slot_halfogre                 // Half Ogre animation - DEPRECATED WRITE_SHORT 0x028 28672

// Orc range weapon, throwing axe
// ------------------------------
COPY ~%MOD_FOLDER%/cre/d9orc01.cre~   ~override~
	WRITE_LONG 0x0244 0x00004000                   // Barbarian kit, gives barbarian bonus (speed, immunity to backstab, resistance)

// Orc range weapon, following Player1
// -----------------------------------
COPY ~%MOD_FOLDER%/cre/d9orgahj.cre~  ~override~
	WRITE_LONG	0x0244 0x00004000                  // Barbarian kit, gives barbarian bonus (speed, immunity to backstab, resistance)
//	ADD_CRE_ITEM ~d9boots~ #0 #0 #0 ~NONE~ ~BOOTS~

// 2 Orogs tell Dig Dag to drink potions coz party is coming, they also drink a potion of absorption
// -------------------------------------------------------------------------------------------------
COPY ~%MOD_FOLDER%/cre/d9orc05.cre~   ~override~
     ~%MOD_FOLDER%/cre/d9orc05b.cre~  ~override~
	ADD_CRE_ITEM ~POTN18~ #1 #0 #0 ~NONE~ ~INV~    // Potion of Absorption
	WRITE_LONG 0x0244 0x00004000                   // Barbarian kit, gives barbarian bonus (speed, immunity backstab, resistance)

// Orc Melee following Player1
// ---------------------------
COPY ~%MOD_FOLDER%/cre/d9orgame.cre~  ~override~
	ADD_CRE_ITEM ~HALB01~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP  // Halberd
//	ADD_CRE_ITEM ~d9boots~ #0 #0 #0 ~NONE~ ~BOOTS~
	WRITE_LONG 0x0244 0x00004000                           // Barbarian kit, gives barbarian bonus (speed, immunity to backstab, resistance)

// Ogre Berserker not following Player1
// ------------------------------------
COPY ~%MOD_FOLDER%/cre/d9ogrga.cre~   ~override~
	SAY NAME1 @101
	SAY NAME2 @101
	WRITE_ASCII SCRIPT_RACE ~~ #8
//	ADD_CRE_ITEM ~d9boots~ #0 #0 #0 ~NONE~ ~BOOTS~  // Moved from the following cre

// Ogre Barbarian not following Player1
// ------------------------------------
COPY ~%MOD_FOLDER%/cre/d9ogrga2.cre~  ~override~
	SAY NAME1 @102
	SAY NAME2 @102
	WRITE_LONG  0x014 600							// XP value (when killed)
//	ADD_CRE_ITEM ~d9boots~ #0 #0 #0 ~NONE~ ~BOOTS~	// Useless : already in CRE file.
	WRITE_LONG  0x0244 0x00004000					// Barbarian kit, gives barbarian bonus (speed, immunity to backstab, resistance)
//	WRITE_ASCII SCRIPT_OVERRIDE ~GENSHT01~			// DEPRECATED as of v4.0.0: added to the file.
	WRITE_ASCII SCRIPT_RACE ~~ #8
	WRITE_ASCII DEATHVAR ~D9OGRGA2~ #32

// Ogre Mage enemy with GENSHT01 script
// ------------------------------------
COPY_EXISTING ~ogrema01.cre~  ~override/d9ogmage.cre~
	WRITE_ASCII SCRIPT_OVERRIDE ~GENSHT01~ #8

// Groumf (L13 Berserker) and Bourrinos (L11 Barbarian) protect TAZOK. d9gro ???
// -----------------------------------------------------------------------------
// NEW in cre file: writes sex variables and DEATHVAR set to D9GROUMT (former D9GROUM4) and D9BOURT (former D9BOUR14)
COPY ~%MOD_FOLDER%/cre/d9groumt.cre~  ~override~
	SAY NAME1 @104
	SAY NAME2 @104
	WRITE_BYTE  0x270 255                                         // Allegiance Enemy
	WRITE_ASCII SCRIPT_OVERRIDE ~D9GROUMT~ #8
	WRITE_ASCII SCRIPT_DEFAULT  ~WTARSGT~ #8
	ADD_CRE_ITEM ~AX1H02~ #1 #0 #0 ~IDENTIFIED~ ~WEAPON1~         // Battle Axe +1
	ADD_CRE_ITEM ~AX1H04~ #30 #0 #0 ~IDENTIFIED~ ~WEAPON2~ EQUIP  // Throwing Axe
	ADD_CRE_ITEM ~SHLD06~ #0 #0 #0 ~NONE~ ~SHIELD~                // Large Shield +1
	ADD_CRE_ITEM ~PLAT14~ #0 #0 #0 ~NONE~ ~ARMOR~                 // Full Plate Mail +1
	ADD_CRE_ITEM ~RING06~ #0 #0 #0 ~NONE~ ~RING~                  // Ring of the Princes +1
	ADD_CRE_ITEM ~POTN52~ #5 #0 #0 ~undroppable~ ~QITEM1~         // 5 Potions of Extra Healing (As of v5.0.0, all potions are flagged undroppable).
	ADD_CRE_ITEM ~POTN14~ #1 #0 #0 ~undroppable~ ~QITEM2~         // Oil of Speed
	ADD_CRE_ITEM ~POTN24~ #1 #0 #0 ~undroppable~ ~QITEM3~         // Potion of Defense
	ADD_CRE_ITEM ~POTN35~ #1 #0 #0 ~undroppable~ ~INV~            // Potion of Magic Shielding (added as of v5.0.0)
	ADD_CRE_ITEM ~POTN41~ #1 #0 #0 ~undroppable~ ~INV~            // Potion of Power
	// Custom soundest
	SAY BATTLE_CRY1             @201 // ~Misérables insectes !~ [d9groua]
	SAY BATTLE_CRY2             @202 // ~Par la fureur de Tempus !~ [d9grou8]
	SAY BATTLE_CRY3             @203 // ~Par la force d'Hutgar !~ [d9grou9]
	SAY DAMAGE                  ~~ [d9groul]
	SAY DYING                   ~~ [d9groum]
	SAY HURT                    ~~ [d9groue]
	SAY SELECT_COMMON1          @204 // ~Ha Ha Ha !~ [d9groug]
	SAY SELECT_COMMON2          @205 // ~Quoi ?!~ [d9grouf]
	SAY SELECT_COMMON3          @202 // ~Par la fureur de Tempus !~ [d9grou8]
	SAY SELECT_COMMON4          @206 // ~Encore du sang !~ [d9grou0]
	SAY CRITICAL_MISS           ~~ [d9grou1]
	SAY TARGET_IMMUNE           ~~ [d9grou2]
	SAY SPELL_DISRUPTED         ~~ [d9grou6]
	SAY REACT_TO_DIE_GENERAL    ~~ [d9grouw]
BUT_ONLY

COPY ~%MOD_FOLDER%/cre/d9bourt.cre~   ~override~
	SAY NAME1 @105
	SAY NAME2 @105
	WRITE_BYTE  0x270 255                                           // Allegiance Enemy
	WRITE_ASCII SCRIPT_OVERRIDE ~D9BOURT~ #8
	ADD_CRE_ITEM ~HALB02~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED // Halberd +1. Modified as of v5.0.0 (was HALB07 - Halberd +2)
	ADD_CRE_ITEM ~CHAN04~ #0 #0 #0 ~NONE~ ~ARMOR~                   // Splint Mail
	ADD_CRE_ITEM ~POTN52~ #5 #0 #0 ~undroppable~ ~QITEM1~           // 5 Potions of Extra Healing (was 3 in v4.0.0). As of v5.0.0, all potions are flagged undroppable.
	ADD_CRE_ITEM ~POTN14~ #1 #0 #0 ~undroppable~ ~QITEM2~           // Oil of Speed
	ADD_CRE_ITEM ~POTN18~ #1 #0 #0 ~undroppable~ ~QITEM3~           // Potion of Absorption
	ADD_CRE_ITEM ~POTN41~ #1 #0 #0 ~undroppable~ ~INV~              // Potion of Power
	ADD_CRE_ITEM ~POTN35~ #1 #0 #0 ~undroppable~ ~INV~              // Potion of Magic Shielding (added as of v5.0.0)
	// Custom soundest
	SAY BATTLE_CRY1     @211 // ~À mort !~ [d9bara9]
	SAY BATTLE_CRY2     @212 // ~Baston !~ [d9baraa]
	SAY BATTLE_CRY3     @212 // ~Baston !~ [d9baraa]
	SAY SELECT_COMMON1  @212 // ~Baston !~ [d9baraa]
	SAY SELECT_COMMON2  @213 // ~Ouais !~ [d9barag]
	SAY SELECT_COMMON3  @214 // ~Quoi ?!~ [d9barah]
	SAY SELECT_COMMON4  @211 // ~À mort !~ [d9bara9]
	SAY DAMAGE          ~~ [d9baral]
	SAY DYING           ~~ [d9baram]
	SAY HURT            ~~ [d9barae]
	SAY CRITICAL_MISS   ~~ [d9bara1]
	SAY SPELL_DISRUPTED ~~ [d9bara6]
BUT_ONLY


/* ---------------------------------- *
 *  Installing Scripts and dialogues  *
 * ---------------------------------- */

// Maybe I should let Groumf and Bourinos drink a potion like Tazok, I will see in the future.
COMPILE ~%MOD_FOLDER%/baf/d9groumt.baf~
        ~%MOD_FOLDER%/baf/d9bourt.baf~
        ~%MOD_FOLDER%/baf/d9orc05.baf~
        ~%MOD_FOLDER%/baf/d9orc05b.baf~

// CutScenes in Firkraag's lair
// ----------------------------
//COPY ~%MOD_FOLDER%/Items/d9nofog.itm~ ~override~	// DEPRECATED af of v4.0.0 : Removed because redundant.
        ~%MOD_FOLDER%/baf/d9cutta.baf~
        ~%MOD_FOLDER%/baf/d9cutta1.baf~
        ~%MOD_FOLDER%/baf/d9cutta2.baf~

// Scripts and dialogues for Tazok and DigDag
// ------------------------------------------
        ~%MOD_FOLDER%/baf/d9tazokk.baf~ // This script enforces Tazok to drink 2 potions before fighting

EXTEND_TOP ~firorc01.bcs~  ~%MOD_FOLDER%/baf/d9firorc.baf~

COMPILE ~%MOD_FOLDER%/dlg/d9firorc.d~  // USING ~%MOD_FOLDER%/lang/%s/d9firorc.tra~


/* DEPRECATED as of v4.0.0 (replaced with function extend_area_script defined in ALWAYS block): sets an area script to AR1203
COPY_EXISTING ~AR1203.are~ ~override~
	WRITE_ASCII 0x0094 ~AR1203~
BUT_ONLY */

// Scripts Extensions (As of v5.0.0, extend top scripts instead of bottom)
// -----------------------------------------------------------------------
LAF ~extend_area_script~ STR_VAR area = "ar1202" top = "%MOD_FOLDER%/baf/d9ar1202.baf" END
LAF ~extend_area_script~ STR_VAR area = "ar1203" top = "%MOD_FOLDER%/baf/d9ar1203.baf" END


/* ---------------------- *
 *  Installing Soundsets  *
 * ---------------------- */

/* DEPRECATED as of v4.0.0: Uses new HANDLE_AUDIO function instead of bat files and adds languages sounds folders.
COPY ~%MOD_FOLDER%/sounds~	~override~

AT_INTERACTIVE_EXIT ~%MOD_FOLDER%/sounds/Audioin.bat~
AT_INTERACTIVE_UNINSTALL ~%MOD_FOLDER%/sounds/Audiouninstall.bat~
*/

ACTION_IF NOT is_soundset BEGIN
	ACTION_IF (FILE_EXISTS ~%MOD_FOLDER%/audio/%LANGUAGE%~) BEGIN
		LAF HANDLE_AUDIO
			STR_VAR audio_path  = EVAL ~butchery/audio/%LANGUAGE%~
					oggdec_path = ~butchery/audio~
					sox_path	= ~butchery/audio~
		END
	END ELSE BEGIN
		LAF HANDLE_AUDIO
			STR_VAR audio_path  = ~butchery/audio/french~
					oggdec_path = ~butchery/audio~
					sox_path	= ~butchery/audio~
		END
	END
END
