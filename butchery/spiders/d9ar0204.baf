/* ========================================================= *
 *    Spiders butchery in Temple City (Unseeing Eye Plot)    *
 * ========================================================= */

IF
	Global("D9addvilain","AR0204",0)
THEN
	RESPONSE #100
		SetGlobal("D9addvilain","AR0204",1)
 		CreateCreature("D9IDSW0B",[1127.2941],12)  // Sword Spider - Araignée sabre
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END

IF
	Global("D9beginbutcheryspiders","AR0204",1)
	Global("D9beginbutcheryspidersmessage","AR0204",0)
THEN
	RESPONSE #100
	    DisplayStringHead(Player1,@14661)  // ~Les ennemis semblent arriver de partout !~
		SetGlobal("D9beginbutcheryspidersmessage","AR0204",1)
END

IF
	Global("D9beginbutcheryspiders","AR0204",1)
	GlobalLT("MaxSpawn","AR0204",40)
	GlobalLT("EXTRACOUNT","MYAREA",4)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR0204",1)
		CreateCreatureOffScreen("D9IDPH01",0)  // Phase Spider - Araignée éclipsante
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR0204",1)
		CreateCreatureOffScreen("D9IDSM01",0)  // Small Spider - Petite araignée
		IncrementGlobal("EXTRACOUNT","MYAREA",1)
END

IF
	Global("D9beginbutcheryspiders","AR0204",1)
	GlobalLT("MaxSpawn","AR0204",40)
	GlobalLT("EXTRACOUNT2","MYAREA",4)
	OR(2)
		Difficulty(HARD)
		Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR0204",1)
		CreateCreatureOffScreen("D9IDSW01",0)  // Sword Spider - Araignée sabre
		IncrementGlobal("EXTRACOUNT2","MYAREA",1)
END

IF
	Global("D9beginbutcheryspiders","AR0204",1)
	GlobalLT("MaxSpawn","AR0204",40)
	GlobalLT("EXTRACOUNT3","MYAREA",2)
	Difficulty(HARDEST)
	ActionListEmpty()
	InActiveArea(Myself)
THEN
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR0204",1)
		CreateCreatureOffScreen("D9IDSM02",0)  // Mutated Spider - Araignée mutante
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
	RESPONSE #100
		IncrementGlobal("MaxSpawn","AR0204",1)
		CreateCreatureOffScreen("D9IDGI03",0)  // Giant Spider - Araignée géante
		IncrementGlobal("EXTRACOUNT3","MYAREA",1)
END
